buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.25.3'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'idea'

def projectName = 'formic'
version "$VERSION_NAME"
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
//api "com.github.tommyettinger:formic:0.1.5"

idea.module.excludeDirs += [file("src/main/java/emu/")]
[compileJava, compileTestJava, javadoc]*.exclude("emu")
// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:all,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

sourceSets.test.resources.srcDirs += [ file('src/test/resources').path ]

javadoc.destinationDir = file('docs/apidocs')

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.jsinterop:base:1.0.1"
    api "com.google.elemental2:elemental2-core:1.2.1"
    api "com.google.elemental2:elemental2-dom:1.2.1"
}
